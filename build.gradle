buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.2'
        classpath 'me.tatarka:gradle-retrolambda:3.1.0'
        classpath 'io.fabric.tools:gradle:1.19.0'
        classpath 'net.sf.proguard:proguard-base:5.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'

def applyProperty(Properties p, String id, String var) {
    if (var != null && var != '' && p[id] != var) {
        p[id] = var;
        return true;
    }

    return false;
}

def getBuildProperties() {
    def Properties p = new Properties()
    def File f = file('build.properties');

    if (f.exists()) {
        FileInputStream fis = new FileInputStream(f);
        p.load(fis);
        fis.close();
    }

    def res = applyProperty(p, 'ods.sign.password', signPassword);
    res = applyProperty(p, 'ods.key.fabric', propFabric) || res;
    res = applyProperty(p, 'ods.repo.user', repoUser) || res;
    res = applyProperty(p, 'ods.repo.password', repoPassword) || res;
    res = applyProperty(p, 'test.acc.url', testUrl) || res;
    res = applyProperty(p, 'test.acc.login', testLogin) || res;
    res = applyProperty(p, 'test.acc.pwd', testPwd) || res;

    if (res) {
        FileOutputStream fos = new FileOutputStream(f);
        p.store(fos, null);
        fos.close();
    }

    return p;
}

def Properties props = getBuildProperties();

retrolambda {
    javaVersion JavaVersion.VERSION_1_7
}

android {
    compileSdkVersion 'android-22'
    buildToolsVersion '22.0.1'

    signingConfigs {
        release {
            storeFile file('keystore')
            keyAlias 'beta'
            storePassword props['ods.sign.password']
            keyPassword props['ods.sign.password']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFile 'proguard-project.txt'
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 22
        versionCode 1
        versionName '2.0.0'
        resConfigs 'en', 'de', 'fr'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        manifestPlaceholders = ['ods.key.fabric': props['ods.key.fabric']]
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'com/j256/ormlite/android/LICENSE.txt'
        exclude 'com/j256/ormlite/android/README.txt'
        exclude 'com/j256/ormlite/core/LICENSE.txt'
        exclude 'com/j256/ormlite/core/README.txt'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }

    maven {
        url "https://artifactory.dataspace.cc/artifactory/libs-release"

        credentials {
            username props['ods.repo.user']
            password props['ods.repo.password']
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.0.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'org.slf4j:slf4j-android:1.7.7'
    compile 'com.crashlytics.sdk.android:crashlytics:2.2.4'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.squareup.picasso:picasso:2.5.2'

    //noinspection GradleDynamicVersion
    compile 'org.apache.chemistry.opencmis:chemistry-opencmis-android-client:0.13.0.126+'

    testCompile 'junit:junit:4.12'
    testCompile 'org.apache.maven:maven-ant-tasks:2.1.3'

    testCompile('org.robolectric:robolectric:3.0-rc3') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}
